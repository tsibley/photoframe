prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix acl: <http://www.w3.org/2001/02/acls#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select distinct ?photo ?created ?jpg

where {
    {
        # Public and family/friend-visible photos
        ?photo a <x-urn:flickr:photo> ;
            acl:accessor ?visibility ;
            dc:created ?created .

        filter (?visibility in ("public", "family", "friend", "family;friend"))

        # Filter out photos of an old girlfriend (tagged lauren, in 2007)
        filter not exists {
            ?photo dc:subject [ a <x-urn:flickr:tag>; skos:prefLabel "lauren" ] .

            bind (xsd:integer(strbefore(?created, "-")) as ?createdYear)
            filter (?createdYear = 2007)
        }

        # Filter out photos with certain tags
        filter not exists {
            ?photo dc:subject [ a <x-urn:flickr:tag>; skos:prefLabel ?tag ] .
            filter (
                ?tag in
                    ("publicaffairs"    # Bad, boring shots for the public affairs office in college
                    ,"blood"            # High school me had some silly noir tendencies.
                    )
            )
        }
    } union {
        # Private photos
        ?photo a <x-urn:flickr:photo> ;
            acl:accessor "private" ;
            dc:created ?created .

        # Filter to private photos with specifically allowed tags
        filter exists {
            ?photo dc:subject [ a <x-urn:flickr:tag>; skos:prefLabel ?tag ] .

            filter (
                ?tag in
                    ("theo"             # â™¥
                    ,"90th"             # Savta's (Amy's) 90th birthday party
                    ,"samantha"         # Samantha at holidays long ago and in Seattle years ago
                    ,"elizabethtown"    # Samantha's graduation
                    ,"polaroid"         # Scanned polaroids
                    )
            )
        }
    }

    # Filter out specific photos which don't get caught by tags
    filter (
        ?photo not in
            (<http://www.flickr.com/photos/35468142996@N01/445344138>
            ,<https://www.flickr.com/photos/35468142996@N01/34442529213>
            ,<https://www.flickr.com/photos/35468142996@N01/35121676171>
            ,<https://www.flickr.com/photos/35468142996@N01/35121674771>
            ,<https://www.flickr.com/photos/35468142996@N01/35121665371>
            ,<https://www.flickr.com/photos/35468142996@N01/35121651681>
            )
    )

    ?jpg rdfs:seeAlso ?photo ;
        dcterms:relation "Original" .
}

order by ?created ?photo

# vim: set syn=sparql:
