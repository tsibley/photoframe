prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix acl: <http://www.w3.org/2001/02/acls#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?photo ?created ?jpg

where {
    # XXX TODO: Eventually use friends/family/private too, but this requires
    # authenticated URLs.
    ?photo a <x-urn:flickr:photo> ;
        acl:accessor "public" ;
        dc:created ?created .

    ?jpg rdfs:seeAlso ?photo ;
        dcterms:relation "Original" .

    # Filter out photos of an old girlfriend (tagged lauren, in 2007)
    filter not exists {
        ?photo dc:subject [ a <x-urn:flickr:tag>; skos:prefLabel "lauren" ] .

        bind (xsd:integer(strbefore(?created, "-")) as ?createdYear)
        filter (?createdYear = 2007)
    }
}

order by ?created ?photo

# vim: set syn=sparql:
